group 'ru.spbau.mit'
version '1.0-SNAPSHOT'

buildscript {
    ext.kotlin_version = '1.1.51'

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'antlr'
apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'application'

sourceCompatibility = 1.8

mainClassName = "ru.spbau.mit.MainKt"

run {
    if (project.hasProperty("appArgs")) {
        args Eval.me(appArgs)
    }
}

repositories {
    mavenCentral()
}

generateGrammarSource {
    maxHeapSize = "64m"
    arguments += ['-package', 'ru.spbau.mit.parser', '-visitor']
    outputDirectory = new File("$buildDir/generated-src/antlr/main/ru/spbau/mit/parser")
}
compileJava.dependsOn generateGrammarSource
compileKotlin.dependsOn generateGrammarSource

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
    compile "org.antlr:antlr4-runtime:4.7"
    antlr "org.antlr:antlr4:4.7"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
